\myChapter{Sintassi di LAPSA in Xtext}\label{app:sintassi:lapsa}
Di seguito riportiamo la definizione della sintassi di \ac{lapsa} tramite \xtext{}

\begin{lstlisting}[language=xtext,style=eclipse,caption={Sintassi di \acs{lapsa} in \xtext{}},label=code:lapsa:syntax]
grammar unifi.marcotinacci.thesis.seal.Seal with org.eclipse.xtext.common.Terminals

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
generate seal "http://www.marcotinacci.unifi/thesis/seal/Seal"

// === main program syntax ===

Program:
	'actions' '{' (actions+=Action)+ '}'
	'subject' modules+=ModuleDefine
	(modules+=ModuleDefine)*
	'environment' ((environment=Environment) | isEmptyEnv?='is empty')
	('ranges' '{' ranges+=Range (',' ranges+=Range)* '}')?
;

Range:
	module=[ModuleDefine] '.' variable=[VariableDeclaration] 'in' '[' from=Value ',' to=Value ']' ('delta' '=' delta=Value)?
;

Action: name=ID ;

// === module syntax ===

ModuleDefine: 
	'module' name=ID '{'
		(variables+=VariableDeclaration ';')+
		(rules+=Rule ';')+
		('target' 'never' never+=Expression)*
	'}'
;

VariableDeclaration:
	type=Type name=ID '=' expr=Expression
;

Type:
	name='int' | name='float' | name='bool'
;

Rule:
	cond=Expression '[' action=[Action] ']' (ndCases+=NDCase)+
;

NDCase:
	'=>' cases+=Case ('#' cases+=Case)*
;

Case:
	'<'weight=Expression (hasCondition?=',' cond=Expression)? '>' update+=Update (',' update+=Update)*
;

Update:
	{ NoAction } 'noaction' | 
	{ Assign } variable=[VariableDeclaration] '=' expr=Expression
;

Environment:
	modules+=[ModuleDefine] ('|{' (actions+=[Action])+ '}|' modules+=[ModuleDefine])*
;


// === expression syntax ===

Expression:
	Logical;

Logical returns Expression:
	Relation
		(({And.left=current} 'and' 
		| {Or.left=current} 'or') 
		right=Relation)*;

Relation returns Expression:
	Addition 
		(({Leq.left=current} '<='
		| {Less.left=current} '<'
		| {Eq.left=current} '=='
		| {Neq.left=current} '!='
		| {Geq.left=current} '>='
		| {Gtr.left=current} '>')
		right=Addition)?;

Addition returns Expression:
	Multiplication 
		(({Plus.left=current} '+' 
		| {Minus.left=current} '-') 
		right=Multiplication)*;

Multiplication returns Expression:
	PrimaryExpression 
		(({Multi.left=current} '*' 
		| {Div.left=current} '/') 
		right=PrimaryExpression)*;

PrimaryExpression returns Expression:
	'(' Expression ')'
	| { Not } 'not' cond=PrimaryExpression
	| { Literal } value=Value
	| { Quantifier } 'exists' name=ID ':' module=[ModuleDefine] 'such that' cond=PrimaryExpression
	| { ExternalReference } module=[Quantifier] '.' variable=[VariableDeclaration] 
	| { LocalReference } ('this' '.')? variable=[VariableDeclaration]   
;

Value: INT | FLOAT | BOOL;

terminal INT returns ecore::EInt: ('0'..'9')+;

terminal FLOAT returns ecore::EDouble:
  ('-')? (INT)* ('.' (INT)+)? |
  ('-')? (INT)+ ('.') | 
  ('-')? (INT)+ ('.' (INT)*)? (('e'|'E')('-'|'+')? (INT)+);

terminal BOOL returns ecore::EBoolean: 'true' | 'false';
\end{lstlisting}

\vspace{0.4cm}